# Area between two curves

```nocode, noout
using Plots
plotly()
```

The definite integral gives the "signed" area between the function $f(x)$ and the $x$-axis over $[a,b]$. Conceptually, this is the area between two curves, $f(x)$ and $g(x)=0$. More generally, this integral:

$$~
\int_a^b (f(x) - g(x)) dx
~$$

can be interpreted as the "signed" area between $f(x)$ and $g(x)$ over
$[a,b]$. If on this interval $[a,b]$ it is true that $f(x) \geq g(x)$,
then this would just be the area, as seen in this figure. The
rectangle in the figure has area: $(f(a)-g(a)) \cdot (b-a)$ which
could be a term in a left Riemann sum of the integral of $f(x) - g(x)$:


```nocode
f1(x) = x^2
g1(x) = sqrt(x)
a,b = 1/4, 3/4

xs = linspace(a, b, 250)
ss = vcat(xs, reverse(xs))
ts = vcat(f1.(xs), g1.(reverse(xs)))

plot(f1, 0, 1, legend=false)
plot!(g1)
plot!(ss, ts, fill=(0, :red))
plot!(xs, f1.(xs), linewidth=5, color=:green)
plot!(xs, g1.(xs), linewidth=5, color=:green)


plot!(xs, f1.(xs), legend=false, linewidth=5, color=:blue)
plot!(xs, g1.(xs), linewidth=5, color=:blue)
u,v = .4, .5
plot!([u,v,v,u,u], [f1(u), f1(u), g1(u), g1(u), f1(u)], color=:black, linewidth=3)
```

For the figure, we have $f(x) = \sqrt{x}$, $g(x)= x^2$ and $[a,b] = [1/4, 3/4]$. The shaded area is then found by:

$$~
\int_{1/4}^{3/4} (x^{1/2} - x^2) dx = (\frac{x^{3/2}}{3/2} - \frac{x^3}{3})\big|_{1/4}^{3/4} = \frac{\sqrt{3}}{4} -\frac{7}{32}.
~$$



## Examples

Find the area bounded by the line $y=2x$ and the curve $y=2 - x^2$.

We can plot to see the area in question:

```
using Plots
f(x) = 2 - x^2
g(x) = 2x
plot([f,g], -3,3)
```

For this problem we need to identify $a$ and $b$. These are found numerically through:

```
using Roots
a,b = fzeros(x -> f(x) - g(x), -3, 3)
```

The answer then can be found numerically:

```
using QuadGK
QuadGK.quadgk(x -> f(x) - g(x), a, b)[1]
```

##### Example

Find the integral between $f(x) = \sin(x)$ and $g(x)=\cos(x)$ over $[0,2\pi]$ where $f(x) \geq g(x)$.

A plot shows the areas:

```
f(x) = sin(x)
g(x) = cos(x)
plot([f,g], 0, 2pi)
```

There is a single interval when $f \geq g$ and this can be found
algebraically using basic trigonometry, or numerically:

```
a,b = fzeros(x -> f(x) - g(x), 0, 2pi)  # pi/4, 5pi/4
QuadGK.quadgk(x -> f(x) - g(x), a, b)[1]
```

##### Example

Find the area between $x^n$ and $x^{n+1}$ over $[0,1]$ for $n=1,2,\dots$.

We have on this interval $x^n \geq x^{n+1}$, so the integral can be found symbolically through:

```
using SymPy
x, n = symbols("x,n", real=true, positive=true)
ex = integrate(x^n - x^(n+1), (x, 0, 1))
together(ex)
```

Based on this answer, what is the value of this 

$$~
\frac{1}{2\cdot 3} + \frac{1}{3\cdot 4} + \frac{1}{4\cdot 5} + \cdots?
~$$

This should should be no surprise, given how the areas computed carve up the area under the line $y=x^1$ over $[0,1]$:

```
summation(1/(n+1)/(n+2), (n, 1, oo))
```

##### Example

Verify [Archimedes'](http://en.wikipedia.org/wiki/The_Quadrature_of_the_Parabola) finding that the area of the parabolic segment is $4/3$rds that of the triangle joining $a$, $(a+b)/2$ and $b$.


```nocode
f(x) = 2 - x^2
a,b = -1, 1/2
c = (a+b)/2
xs = linspace(-sqrt(2), sqrt(2))
rxs = linspace(a, b)
rys = map(f, rxs)


plot(f, a, b, legend=false, linewidth=3)
xs = [a,c,b,a]
plot!(xs, f.(xs), linewidth=3)
```

For concreteness, let $f(x) = 2-x^2$ and $[a,b] = [-1, 1/2]$, as in
the figure. Then the area of the triangle can be computed through:

```
f(x) = 2 - x^2
a,b = -1, 1/2
c = (a +b)/2
function secant(f, x1, x2)
   m = (f(x2) - f(x1)) / (x2 - x1)
   x -> f(x1) + m * (x-x1)
end

sac, sab, scb = secant(f, a,c), secant(f, a,b), secant(f, c,b)
f1(x) = min(sac(x), scb(x))
f2(x) = sab(x)

val1 = QuadGK.quadgk(x -> f1(x) - f2(x), a, b)[1]
```

As we needed three secant lines, we created a function to generate
them. Once that was done, we used the `max` function to facilitate
integrating over the top bounding curve, alternatively, we could break
the integral over $[a,c]$ and $[c,b]$.

The area of the parabolic segment is more straightforward.

```
val2 = QuadGK.quadgk(x -> f(x) - f2(x), a, b)[1]
```

Finally, if Archimedes was right, this relationship should bring about $0$ (or something within round-off error):

```
val1 * 4/3 - val2
```

##### Example

Find the area bounded by $y=x^4$ and $y=e^x$ when $x^4 \geq e^x$ and
$x > 0$.

A graph  over $[0,10]$ shows clearly the largest zero, for afterwards
the exponential dominates the power. 

```
f(x) = x^4
g(x) = exp(x)
plot([f, g], 0, 10)
```

There must be another zero, though it is hard to see from the graph over $[0,10]$,
as $0^4=0$ and $e^0=1$, so the polynomial must cross below the
exponential to the left of $5$. (Otherwise, plotting over $[0,2]$ will
clearly reveal the other zero.) We now find these intersection points
numerically and then integrate:

```
a,b = fzeros(x -> f(x) - g(x), 0, 10)
QuadGK.quadgk(x -> f(x) - g(x), a, b)[1]
```

##### Examples

The area between $y=\sin(x)$ and $y=m\cdot x$ between $0$ and the
first positive intersection depends on $m$ (where $0 \leq m \leq
1$. The extremes are when $m=0$, the area is $2$ and when $m=1$ (the
line is tangent at $x=0$), the area is $0$. What is it for other
values of $m$? The picture for $m=1/2$ is:

```
m = 1/2
plot(sin, 0, pi)
plot!(x -> m*x, 0, pi)
```

For a given $m$, the area is found after computing $b$, the
intersection point. We express this as a function of $m$ for later reuse:

```
B(m) = maximum(fzeros(x -> sin(x) - m*x, 0, pi))
a = 0
b = B(m)
QuadGK.quadgk(x -> sin(x) - m*x, a, b)[1]
```


In general, the area then as a function of `m` is found by substitution `B(m)` for `b`:

```
area(m) = QuadGK.quadgk(x -> sin(x) - m*x, 0, B(m))[1]
```

A plot shows the relationship:

```
plot(area, 0, 1)
```

While here, let's also answer the question of which $m$ gives an area
of $1$, or one-half the total? This can be done as follows:

```
fzero(m -> area(m) - 1, 0, 1)
```

(Which is a nice combination of using `fzeros`, `QuadGK.quadgk` and `fzero` to answer a problem.)

##### Example

Find the area bounded by the $x$ axis, the line $x-1$ and the function $\log(x+1)$.

A plot shows us the basic area:

```
f(x) = log(x+1)
g(x) = x - 1
plot([f,g,zero],0, 3)
```

The value for "$b$" is found from the intersection point of  $\log(x+1)$ and $x-1$, which is near $2$:

```
a = 0
b = fzero(x -> f(x) - g(x), 2)
```

We see that the lower part of the area has a condition: if $x < 1$ then use $0$, otherwise use $g(x)$. We can handle this many different ways:

* break the integral into two pieces and add:

```
QuadGK.quadgk(x -> f(x) - zero(x), a, 1)[1] + QuadGK.quadgk(x -> f(x) - g(x), 1, b)[1]
```

* make a new function for the bottom bound:

```
h(x) = x < 1 ? 0.0 : g(x)
QuadGK.quadgk(x -> f(x) - h(x), a, b)[1]
```

* Turn the picture on its side and integrate in the $y$ variable. To
  do this, we need to solve for inverse functions:

```
a1=f(a)
b1=f(b)
f1(y)=y+1                # y=x-1, so x=y+1
g1(y)=exp(y)-1           # y=log(x+1) so e^y = x + 1, x = e^y - 1
QuadGK.quadgk(y -> f1(y) - g1(y), a1, b1)[1]
```

```
note("""

When doing problems by hand this latter style can often reduce the complications, but when approaching the task numerically, the first two styles are generally easier, though computationally more expensive.

""")
```

#### Integrating in different directions

The last example suggested integrating in the $y$ variable. This could have more explanation.

It has been noted that different symmetries can aid in computing
integrals through their interpretation as areas. For example, if
$f(x)$ is odd, then $\int_{-b}^b f(x)dx=0$ and if $f(x)$ is even,
$\int_{-b}^b f(x) dx = 2\int_0^b f(x) dx$.

Another symmetry of the $x-y$ plane is the reflection through the line
$y=x$. This has the effect of taking the graph of $f(x)$ to the graph
of $f^{-1}(x)$ and vice versa. Here is an example with $f(x) = x^3$
over $[-1,1]$.

```
f(x) = x^3
xs = linspace(-1, 1)
ys = f.(xs)
plot(ys, xs)
```

By switching the order of the `xs` and `ys` we "flip" the graph
through the line $x=y$. 

We can use this symmetry to our advantage. Suppose instead of being given an equation $y=f(x)$, we are given it in "inverse" style: $x = f(y)$, for example suppose we have $x = y^3$. We can plot this as above via:

```
ys = linspace(-1,1)
xs = [y^3 for y in ys]
plot(xs, ys)
```

Suppose we wanted the area in the first quadrant between this graph,
the $y$ axis and the line $y=1$. What to do? With the problem
"flipped" through the $y=x$ line, this would just be $\int_0^1 x^3dx$.
Rather than mentally flipping the picture to integrate, instead we can just integrate in
the $y$ variable. That is, the area is  $\int_0^1 y^3 dy$. The
mental picture for Riemann sums would be have the approximating rectangles laying flat and as
a function of $y$, are given a length of $y^3$ and height of "$dy$".

For a less trivial problem, consider the area between $x = y^2$ and $x
= 2-y$ in the first quadrant.

```
ys = linspace(0, 2)
xs = [y^2 for y in ys]
plot(xs, ys)
xs = [2-y for y in ys]
plot!(xs, ys)
plot!(zero, 0, 2)
```

We see the bounded area could be described in the "$x$" variable in
terms of two integrals, but in the $y$ variable in terms of the
difference of two functions with the limits of integration running
from $y=0$ to $y=1$. So, this area may be found as follows:

```
f(y) = 2-y
g(y) = y^2
a, b = 0, 1
QuadGK.quadgk(y -> f(y) - g(y), a, b)[1]
```


## Questions

###### Question

Find the area enclosed by the curves $y=2-x^2$ and $y=x^2 - 3$.

```
using Roots
f(x) = 2 - x^2
g(x) = x^2 - 3
a,b = fzeros(x -> f(x) - g(x), -10,10)
val, _ = QuadGK.quadgk(x -> f(x) - g(x), a, b)
numericq(val)
```

###### Question

Find the area between $f(x) = \cos(x)$, $g(x) = x$ and the $y$ axis.

```
f(x) = cos(x)
g(x) = x
a = 0
b = fzero(x -> f(x) - g(x), 1)
val, _ = QuadGK.quadgk(x -> f(x) - g(x), a, b)
numericq(val)
```

###### Question

Find the area between the line $y=1/2(x+1)$ and half circle $y=\sqrt{1 - x^2}$.

```
f(x) = sqrt(1 - x^2)
g(x) = 1/2 * (x + 1)
a,b = fzeros(x -> f(x) - g(x), -1, 1)
val, _ = QuadGK.quadgk(x -> f(x) - g(x), a, b)
numericq(val)
```

###### Question

Find the area in the first quadrant between the lines $y=x$, $y=1$, and the curve $y=x^2 + 4$.

```
f(x) = x
g(x) = 1.0
h(x) = min(f(x), g(x))
j(x) = x^2 / 4
a,b = fzeros(x -> h(x) - j(x), 0, 3)
val, _ = QuadGK.quadgk(x -> h(x) - j(x), a, b)
numericq(val)
```

###### Question

Find the area between $y=x^2$ and $y=-x^4$ for $\lvert x \rvert \leq 1$.

```
f(x) = x^2
g(x) = -x^4
a,b = -1, 1
val, _ = QuadGK.quadgk(x -> f(x) - g(x), a, b)
numericq(val)
```

###### Question

Let `f(x) = 1/(sqrt(pi)*gamma(1/2)) * (1 + t^2)^(-1)` and `g(x) = 1/sqrt(2*pi) * exp(-x^2/2)`. These graphs intersect in two points. Find the area bounded by them.

```
f(x) = 1/(sqrt(pi)*gamma(1/2)) * (1 + x^2)^(-1)
g(x) = 1/sqrt(2*pi) * exp(-x^2/2)
a,b =  fzeros(x -> f(x) - g(x), -3, 3)
val, _ = QuadGK.quadgk(x -> f(x) - g(x), a, b)
numericq(val)
```

(Where `gamma(1/2)` is a call to the [gamma](http://en.wikipedia.org/wiki/Gamma_function) function.)

###### Question

Find the area in the first quadrant bounded by the graph of $x = (y-1)^2$, $x=3-y$ and $x=2\sqrt{y}$. (Hint: integrate in the $y$ variable.)

```
f(y) = (y-1)^2
g(y) = 3 - y
h(y) = 2sqrt(y)
a = 0
b = fzero(y -> f(y) - g(y), 2)
f1(y) = max(f(y), zero(y))
g1(y) = min(g(y), h(y))
val, _ = QuadGK.quadgk(y -> g1(y) - f1(y), a, b)
numericq(val)
```

###### Question

Find the total area bounded by the lines $x=0$, $x=2$ and the curves $y=x^2$ and $y=x$. This would be $\int_a^b \lvert f(x) - g(x) \rvert dx$.

```
f(x) = x^2
g(x) = x
a, b = 0, 2
val, _ = QuadGK.quadgk(x -> abs(f(x) - g(x)), a, b)
numericq(val)
```

###### Question


Look at the sculpture
[Le Tamanoir](https://www.google.com/search?q=Le+Tamanoir+by+Calder.&num=50&tbm=isch&tbo=u&source=univ&sa=X&ved=0ahUKEwiy8eO2tqzVAhVMPz4KHXmgBpgQsAQILQ&biw=1556&bih=878)
by Calder. A large scale work. How much does it weigh? Approximately?


Let's try to answer that with an educated guess. The right most figure
looks to be about 1/5th the total amount. So if we estimate that piece
and multiply by 5 we get a good guess. That part looks like an area of
metal bounded by two quadratic polynomials. If we compute that area in
square inches, then multiply by an assumed thickness of one inch, we
have the cubic volume. The density of galvanized steel is 7850
kg/$m^3$ which we convert into pounds/in$^3$ via:

```
7850 * 2.2 * (1/39.3)^3
```

The two parabolas, after rotating, might look like the following (with $x$ in inches):

$$~
f(x) = x^2/70, \quad g(x) = 35 + x^2/140
~$$



Put this altogether to give an estimated weight in pounds.

```
f(x) = x^2/70
g(x) = 35 + x^2/140
a,b = fzeros(x -> f(x) - g(x), -100, 100)
ar, _ = QuadGK.quadgk(x -> abs(f(x) - g(x)), a, b)
val = 5 * ar * 7850 * 2.2 * (1/39.3)^3
numericq(val)
```

Is the guess that the entire sculpture is more than two tons?

```
choices=["Less than two tons", "More than two tons"]
ans = 2
radioq(choices, ans, keep_order=true)
```


###### Question

Formulas from the business world say that revenue is the integral of *marginal revenue* or the additional money from  selling 1 more unit. (This is basically the derivative of profit). Cost is the integral of *marginal cost*, or the cost to produce 1 more. Suppose we have

$$~
\text{mr}(x) = 2 - \frac{e^{-x/10}}{1 + e^{-x/10}}, \quad
\text{mc}(x) = 1 - \frac{1}{2} \cdot \frac{e^{-x/5}}{1 + e^{-x/5}}.
~$$

Find the profit to produce 100 units: $P = \int_0^{100} (\text{mr}(x) - \text{mc}(x)) dx$.

```
mr(x) = 2 + e^(-x/10) / (1 + e^(-x/10))
mc(x) = 1 + (1/2) * e^(-x/5) / (1 + e^(-x/5))
a, b = 0, 100
val, _ = QuadGK.quadgk(x -> mr(x) - mc(x), 0, 100)
numericq(val)
```


