# Polar Coordinates and Curves

```nocode, noout
using Plots, QuadGK
plotly()
```

The description of the $x$-$y$ plane via Cartesian coordinates is not
the only possible way, though one that is most familiar. Here we discuss
a different means. Instead of talking about over and up from an
origin, we focus on a direction and a distance from the origin.

## Definition of polar coordinates

Polar coordinates parameterize the plane though an angle $\theta$ made from the positive ray of the $x$ axis and a radius $r$. To recover the Cartesian coordinates from the pair $(r,\theta)$, we have these formulas from [right](http://en.wikipedia.org/wiki/Polar_coordinate_system#Converting_between_polar_and_Cartesian_coordinates) triangle geometry:

$$~
x = r \cos(\theta),~ y = r \sin(\theta).
~$$

Each point $(x,y)$ corresponds to several possible values of
$(r,\theta)$, as any integer multiple of $2\pi$ added to $\theta$ will
describe the same point. Except for the origin, there is only one pair
when we restrict to $r > 0$ and $0 \leq \theta < 2\pi$.

For values in the first and fourth quadrants (the range of
$\tan^{-1}(x)$), we have:

$$~
r = \sqrt{x^2 + y^2},~ \theta=\tan^{-1}(y/x).
~$$

For the other two quadrants, the signs of $y$ and $x$ must be
considered. This is done with the function `atan2`.


For example, $(-3, 4)$ would have polar coordinates:

```
x,y = -3, 4
rad, theta = sqrt(x^2 + y^2), atan2(y, x)
```

And reversing

```
rad*cos(theta), rad*sin(theta)
```

This figure illustrates:

```
using Plots
p = plot([-5,5], [0,0],  color=:blue, legend=false)
plot!([0,0],  [-5,5], color=:blue)
plot!([-3,0], [4,0])
scatter!([-3], [4])
annotate!([(-3+0.25, 4.1, "(-3,4) Cartesian or (5, 2.21...) polar")])
Outputonly(p)
``` 


The case where $r < 0$ is handled by going 180 degrees in the opposite direction, in other
words the point $(r, \theta)$ can be described as well by $(-r,\theta+\pi)$.

## Parameterizing curves using polar coordinates

If $r=r(\theta)$, then the parameterized curve $(r(\theta), \theta)$
is just the set of points generated as $\theta$ ranges over some set
of values. There are many examples of parameterized curves that
simplify what might be a complicated presentation in Cartesian coordinates.

For example, a circle has the form $x^2 + y^2 = R^2$. Whereas
parameterized by polar coordinates it is just $r(\theta) = R$, or a
constant function.

The circle centered at $(r_0, \gamma)$ (in polar coordinates) with
radius $R$ has a more involved description in polar coordinates:

$$~
r(\theta) = r_0 \cos(\theta - \gamma)  + \sqrt{R^2 - r_0^2\sin(\theta - \gamma)}.
~$$

The case where $r_0 > R$ will not be defined for all values of $\theta$.

## Examples

To help visualize some polar curves, we  first define a helper function for plotting:

```
polar_plot(r, a=0, b=2pi; kwargs...) = plot(t -> r(t)*cos(t), t -> r(t)*sin(t), a, b; kwargs...)
polar_plot!(r, a=0, b=2pi; kwargs...) = plot!(t -> r(t)*cos(t), t -> r(t)*sin(t), a, b; kwargs...)
```

Then we can plot circles with:

```
R=4; r(t) = R; polar_plot(r, 0, 2pi)

function general_circle(r0, gamma, R)
   function(t)
   disc = R^2 - r0^2 * sin(t-gamma)^2
   disc >= 0 ? r0 * cos(t - gamma) + sqrt(disc) : NaN 
   end
end

polar_plot!(general_circle(2, 0, 2), 0, 2pi)
polar_plot!(general_circle(3, 0, 1), 0, 2pi)
```


##### Example

The [rhodenea](http://www-history.mcs.st-and.ac.uk/Curves/Rhodonea.html) curve has

$$~
r(\theta) = a \sin(k\theta)
~$$

```
a, k = 4, 5
r(theta) = a * sin(k * theta)
polar_plot(r, 0, pi)
```

This graph has radius $0$ whenever $\sin(k\theta) = 0$ or $k\theta
=n\pi$. Solving means that it is $0$ at integer multiples of
$\pi/k$. In the above, with $k=5$, there will $5$ zeroes in
$[0,\pi]$. The entire curve is traced out over this interval, the
values from $\pi$ to $2\pi$ yield negative value of $r$, so are
related to values within $0$ to $\pi$ via the relation $(r,\pi
+\theta) = (-r, \theta)$.

##### Example

The [folium](http://www-history.mcs.st-and.ac.uk/Curves/Folium.html)
is a somewhat similar looking curve, but has this description:

$$~
r(\theta) = -b \cos(\theta) + 4a \cos(\theta) \sin(2\theta)
~$$


```
a, b= 4, 2
r(theta) = -b * cos(theta) + 4a * cos(theta) * sin(2theta)
polar_plot(r)
```

The folium has radial part $0$ when $\cos(\theta) = 0$ or
$\sin(2\theta) = b/4a$. This could be used to find out what values
correspond to which loop. For our choice of $a$ and $b$ this gives $\pi/2$, $3\pi/2$ or, as
$b/4a = 1/8$, when $\sin(2\theta) = 1/8$ which happens at
$a_=0.0626...$ and $\pi/2 - a_0$, $\pi+a_0$ and $3\pi/2 - a_0$. The first folium can be plotted with:

```
a0 = (1/2) * asin(1/8)
polar_plot(r, a0, pi/2-a0)
```


The second---which doesn't appear in the initial plot without zooming in---with

```
polar_plot(r, pi/2-a0, pi/2)
```

The third with

```
polar_plot(r, pi/2, pi+a0)
```

The plot repeats from there, so the initial plot could have been made over $[0, \pi + a_0]$.

##### Example

The [Limacon of Pascal](http://www-history.mcs.st-and.ac.uk/Curves/Limacon.html) has

$$~
r(\theta) = b + 2a\cos(\theta)
~$$

```
a,b = 4, 2
r(theta) = b + 2a*cos(theta)
polar_plot(r)
```

##### Example

Some curves require a longer parameterization, such as this where we
plot over $[0, 8\pi]$ so that the cosine term can range over an entire
half period:

```
r(theta) = sqrt(abs(cos(theta/8)))
polar_plot(r, 0, 8pi)
```

## Area of polar graphs

Consider the [cardiod](http://www-history.mcs.st-and.ac.uk/Curves/Cardioid.html) described by $r(\theta) = 2(1 + \cos(\theta))$:

```
r(theta) = 2(1 + cos(theta))
polar_plot(r, 0, 2pi)
```

How much area is contained in the graph?

In some cases it might be possible to translate back into Cartesian
coordinates and compute from there. In practice, this is not usually the best
solution.

The area can be approximated by wedges (not rectangles). See this part
of a
[Wikipedia](http://en.wikipedia.org/wiki/Polar_coordinate_system#Integral_calculus_.28area.29)
page for a figure.

Imagine we have $a < b$ and a partition $a=t_0 < t_1 < \cdots < t_n =
b$. Then the wedge formed by the area traced by the rays from the
origin to $(r(t_{i-1}), t_{i-1})$ and $(r(t_i), t_i)$ is approximated
by a wedge with constant radius and angle $t_i - t_{i-1}$.  The area
of a circle is $\pi r^2$, so the area of a wedge of angle $\Delta$
would be $\pi r^2 \cdot \Delta/(2\pi) = (1/2)r^2 \Delta$. With our
notation, this is $(1/2) r(t_i)^2 (t_i - t_{i-1})$. Summing these
over the partition gives a Riemann sum approximation for the integral $(1/2)\int_a^b
r(\theta)^2 d\theta$.

> Area of polar regions. Let $R$ denote the region bounded by the curve $r(\theta)$ and bounded by the rays
> $\theta=a$ and $\theta=b$ with $b-a \leq 2\pi$, then the area of $R$ is given by
> $$~A = \frac{1}{2}\int_a^b r(\theta)^2 d\theta.~$$

So the area of the cardiod, which is parameterized over $[0, 2\pi]$ is found by

```
using SymPy
r(theta) = 2(1 + cos(theta))
theta = symbols("theta")
(1//2) * integrate(r(theta)^2, (theta, 0, 2PI))
```

##### Example

The folium has general formula $r(\theta) = -b \cos(\theta)
+4a\cos(\theta)\sin(\theta)^2$. When $a=1$ and $b=1$ a leaf of the
folium is traced out between $\pi/6$ and $\pi/2$. What is the area of
that leaf?


An antiderivative exists for arbitrary $a$ and $b$:

```
a,b = symbols("a,b")
r(theta) = -b*cos(theta) + 4a*cos(theta)*sin(theta)^2
integrate(r(theta)^2, theta) / 2
```

For our specific values, the answer can be computed with:

```
ex = integrate(r(theta)^2, (theta, PI/6, PI/2)) / 2
ex(a => 1, b=>1)
```


###### Example

Pascal's
[limacon](http://www-history.mcs.st-and.ac.uk/Curves/Limacon.html) is
like the cardiod, but contains an extra loop. When $a=1$ and $b=1$ we
have this graph.

```
#ts = vcat(linspace(0, pi/2+pi/6), linspace(3pi/2 - pi/6, pi/2 + pi/6), linspace(3pi/2 - pi/6, 2pi))
a,b = 1,1
r(theta) = b + 2a*cos(theta)
p = plot(t->r(t)*cos(t), t->r(t)*sin(t), 0, pi/2 + pi/6,  legend=false, color=:blue)
plot!(p, t->r(t)*cos(t), t->r(t)*sin(t), 3pi/2 - pi/6, pi/2 + pi/6, color=:orange)
plot!(p, t->r(t)*cos(t), t->r(t)*sin(t), 3pi/2 - pi/6, 2pi, color=:blue)

##p = Gadfly.plot(x=[r(t)*cos(t) for t in ts], y=[r(t)*sin(t) for t in ts],
#         Gadfly.Geom.polygon(fill=true, preserve_order=true),
#         Gadfly.Theme(default_color=colorant"orange"))
Outputonly(p)
```

What is the area contained in the outer loop, that is not in the inner loop?

To answer, we need to find out what range of values in $[0, 2\pi]$ the
inner and outer loops are traced. This will be when $r(\theta) = 0$,
which for the choice of $a$ and $b$ solves $1 + 2\cos(\theta) = 0$, or
$\cos(\theta) = -1/2$. This is $\pi/2 + \pi/6$ and $3\pi/2 -
\pi/6$. The inner loop is traversed between those values and has area:

```
a,b = symbols("a,b")
r(theta) =  b + 2a*cos(theta)
ex = integrate(r(theta)^2 / 2, (theta, PI/2 + PI/6, 3PI/2 - PI/6))
inner = ex(a=>1, b=>1)
```

The outer area (including the inner loop) is the integral from $0$ to $\pi/2 + \pi/6$ plus that from $3\pi/2 - \pi/6$ to $2\pi$. These areas are equal, so we double the first:

```
ex = 2 * integrate(r(theta)^2 / 2, (theta, 0, PI/2 + PI/6))
outer = ex(a=>1, b=>1)
```

The answer is the difference:

```
outer - inner
```

## Arc length

The length of the arc traced by a polar graph can also be expressed
using an integral. Again, we partition the interval $[a,b]$ and
consider the wedge from $(r(t_{i-1}), t_{i-1})$ to $(r(t_i),
t_i)$. The curve this wedge approximates will have its arc length
approximated by the line segment connecting the points. Expressing the
points in Cartesian coordinates and simplifying gives the distance
squared as:

$$~
\begin{align}
d_i^2 &= (r(t_i) \cos(t_i) - r(t_{i-1})\cos(t_{i-1}))^2 + (r(t_i) \sin(t_i) - r(t_{i-1})\sin(t_{i-1}))^2\\
&= r(t_i)^2 - 2r(t_i)r(t_{i-1}) \cos(t_i - t_{i-1}) +  r(t_{i-1})^2 \\
&\approx r(t_i)^2 - 2r(t_i)r(t_{i-1}) (1 - \frac{(t_i - t_{i-1})^2}{2} +  r(t_{i-1})^2 \quad(\text{as} \cos(x) \approx 1 - x^2/2)\\
&= (r(t_i) - r(t_{i-1}))^2 + r(t_i)r(t_{i-1}) (t_i - t_{i-1})^2.
\end{align}
~$$

As was done with arc length we multiply $d_i$ by $ (t_i - t_{i-1}) /
(t_i - t_{i-1})$ and move the bottom factor under the square root:


$$~
\begin{align}
d_i
&= d_i \frac{t_i - t_{i-1}}{t_i - t_{i-1}} \\
&\approx \sqrt{\frac{(r(t_i) - r(t_{i-1}))^2}{(t_i - t_{i-1})^2} +
\frac{r(t_i)r(t_{i-1}) (t_i - t_{i-1})^2}{(t_i - t_{i-1})^2}} \cdot (t_i - t_{i-1})\\
&= \sqrt{(r'(\xi_i))^2 + r(t_i)r(t_{i-1})} \cdot (t_i - t_{i-1}).\quad(\text{the mean value theorem})
\end{align}
~$$

Adding the approximations to the $d_i$ looks like a Riemann sum approximation to the
integral $\int_a^b \sqrt{(r'(\theta)^2) + r(\theta)^2} d\theta$ (with
the extension to the Riemann sum formula needed to derive the arc
length for a parameterized curve). That is:

> Arc length of a polar curve. The arc length of the curve described in polar coordinates by $r(\theta)$ for $a \leq \theta \leq b$ is given by
> $$~\int_a^b \sqrt{(r'(\theta)^2) + r(\theta)^2} d\theta.~$$

We test this out on a circle with $r(\theta) = R$, a constant. The
integrand simplifies to just $\sqrt{R^2}$ and the integral is from $0$
to $2\pi$, so the arc length is $2\pi R$, precisely the formula for
the circumference.

##### Example

A cardiod is described by $r(\theta) = 2(1 + \cos(\theta))$. What is the arc length from $0$ to $2\pi$?

The integrand is integrable

```
r(theta) = 2*(1 + cos(theta))
ds = sqrt(diff(r(theta), theta)^2 + r(theta)^2) |> simplify
```

with antiderivative $4\sqrt{2\cos(\theta) + 2} \cdot \tan(\theta/2)$,
but `SymPy` isn't able to find it. Instead we give a numeric answer:

```
using Roots, QuadGK
Base.ctranspose(f::Function) = D(f)
QuadGK.quadgk(t -> sqrt(r'(t)^2 + r(t)^2), 0, 2pi)[1]
```

##### Example

The [equiangular](http://www-history.mcs.st-and.ac.uk/Curves/Equiangular.html) spiral has polar representation

$$~
r(\theta) = a e^{\theta \cot(b)}
~$$

With $a=1$ and $b=\pi/4$, find the arc length traced out from $\theta=0$ to $\theta=1$.

```
a, b= 1, PI/4
r(theta) = a * exp(theta * cot(b))
ds = sqrt(diff(r(theta), theta)^2 + r(theta)^2)
integrate(ds, (theta, 0, 1))
```


##### Example

An Archimedean [spiral](http://en.wikipedia.org/wiki/Archimedean_spiral) is defined in polar form by

$$~
r(\theta) = a + b \theta
~$$

That is, the radius increases linearly. The crossings of the positive $x$ axis occur at $a + b n 2\pi$, so are evenly spaced out by $2\pi b$. These could be a model for such things as coils of materials of uniform thickness.

For example, a roll of toilet paper promises 1000 sheets with the
[smaller](http://www.phlmetropolis.com/2011/03/the-incredible-shrinking-toilet-paper.php)
$4.1 \times 3.7$ inch size. This $3700$ inch long connected sheet of
paper is wrapped around a paper tube in an Archimedean spiral with
$r(\theta) = d_{\text{inner}}/2 + b\theta$. The entire roll must fit in a standard
dimension, so the outer diameter will be $d_{\text{outer}} = 5~1/4$ inches. Can we figure out
$b$?

Let $n$ be the number of windings and assume the starting and ending point is on the positive $x$ axis,
$r(2\pi n) = d_{\text{outer}}/2 = d_{\text{inner}}/2 + b (2\pi n)$. Solving for $n$ in terms of $b$ we get:
$n = ( d_{\text{outer}} - d_{\text{inner}})/2 / (2\pi b)$. With this, the following must hold as the total arc length is $3700$ inches.


$$~
\int_0^{n\cdot 2\pi} \sqrt{r(\theta)^2 + r'(\theta)^2} d\theta = 3700
~$$

Numerically then we have:

```
using Roots
dinner = 1 + 5/8
douter = 5 + 1/4
r(b,t) = dinner/2 + b*t
rp(b,t) = b
integrand(b,t) = sqrt((r(b,t))^2 + rp(b,t)^2)  # sqrt(r^2 + r'^2)
n(b) = (douter - dinner)/2/(2*pi*b)
b = fzero(b -> QuadGK.quadgk(t->integrand(b,t), 0, n(b)*2*pi)[1] - 3700, 1/100000, 1/100)
```

This gives a value in inches, in millimeters this is about a $.02mm$ thickness per sheet:

```
b * 25.4
```


## Questions

###### Question

Let $r=3$ and $\theta=\pi/8$. In Cartesian coordinates what is $x$?

```
x,y = 3 * [cos(pi/8), sin(pi/8)]
numericq(x)
```

What is $y$?

```
numericq(y)
```

###### Question

A point in Cartesian coordinates is given by $(-12, -5)$. In has a polar coordinate representation with an angle $\theta$ in $[0,2\pi]$ and $r > 0$. What is $r$?

```
x,y = -12, -5
r1, theta1 = sqrt(x^2 + y^2), atan2(y,x)
numericq(r1)
```

What is $\theta$?

```
numericq(theta1)
```

###### Question

Does $r(\theta) = a \sec(\theta - \gamma)$ describe a line for $0$ when $a=3$ and $\gamma=\pi/4$?

```
yesnoq("yes")
```

If yes, what is the $y$ intercept 

```
r(theta) == 3 * sec(theta -pi/4)
val = r(pi/2)
numericq(val)
```

What is slope of the line?

```
val = (r(pi/2)*sin(pi/2) - r(pi/4)*sin(pi/4)) / (r(pi/2)*cos(pi/2) - r(pi/4)*cos(pi/4))
numericq(val)
```

Does this seem likely: the slope is $-1/\tan(\gamma)$?

```
yesnoq("yes")
```

###### Question

The polar curve $r(\theta) = 2\cos(\theta)$ has tangent lines at most points. This differential representation of the chain rule

$$~
\frac{dy}{dx} = \frac{dy}{d\theta} / \frac{dx}{d\theta},
~$$

allows the slope to be computed when $y$ and $x$ are the Cartesian
form of the polar curve. For this curve, we have

$$~
\frac{dy}{d\theta} = \frac{d}{d\theta}(2\cos(\theta) \cdot \cos(\theta)),~ \text{ and }
\frac{dx}{d\theta} = \frac{d}{d\theta}(2\sin(\theta) \cdot \cos(\theta)).
~$$

Numerically, what is the slope of the tangent line when $\theta = \pi/4$?

```
using Roots
r(theta) = 2cos(theta)
g(theta) = r(theta)*cos(theta)
f(theta) = r(theta)*sin(theta)
c = pi/4
val = D(g)(c) / D(f)(c)
numericq(val)
```

###### Question

For different values $k > 0$ and $e > 0$ the polar equation

$$~
r(\theta) = \frac{ke}{1 + e\cos(\theta)}
~$$

has a familiar form. The value of $k$ is just a scale factor, but different values of $e$ yield different shapes.

When $0 < e < 1$ what is the shape of the curve? (Answer by making a plot and guessing.)

```
choices = [
"an ellipse",
"a parabola",
"a hyperbola",
"a circle",
"a line"
]
ans = 1
radioq(choices, ans, keep_order=true)
```


When $e = 1$ what is the shape of the curve?

```
choices = [
"an ellipse",
"a parabola",
"a hyperbola",
"a circle",
"a line"
]
ans = 2
radioq(choices, ans, keep_order=true)
```


When $1 < e$ what is the shape of the curve?

```
choices = [
"an ellipse",
"a parabola",
"a hyperbola",
"a circle",
"a line"
]
ans = 3
radioq(choices, ans, keep_order=true)
```

###### Question

Find the area of a lobe of the
[lemniscate](http://www-history.mcs.st-and.ac.uk/Curves/Lemniscate.html)
curve traced out by $r(\theta) = \sqrt{\cos(2\theta)}$ between
$-\pi/4$ and $\pi/4$. What is the answer?

```
choices = [
L"1/2",
L"\pi/2",
L"1"
]
ans=1
radioq(choices, ans)
```

###### Question

Find the area of a lobe of the [eight](http://www-history.mcs.st-and.ac.uk/Curves/Eight.html) curve traced out by $r(\theta) = \cos(2\theta)\sec(\theta)^4$ from $-\pi/4$ to $\pi/4$. Do this numerically.

```
r(theta) = sqrt(cos(2theta) * sec(theta)^4)
val, _ = QuadGK.quadgk(t -> r(t)^2/2, -pi/4, pi/4)
numericq(val)
```

###### Question

Find the arc length of a lobe of the
[lemniscate](http://www-history.mcs.st-and.ac.uk/Curves/Lemniscate.html)
curve traced out by $r(\theta) = \sqrt{\cos(2\theta)}$ between
$-\pi/4$ and $\pi/4$. What is the answer (numerically)?

```
using Roots
r(theta) = sqrt(cos(2theta))
val, _ = QuadGK.quadgk(t -> sqrt(D(r)(t)^2 + r(t)^2), -pi/4, pi/4)
numericq(val)
```

###### Question


Find the arc length of a lobe of the [eight](http://www-history.mcs.st-and.ac.uk/Curves/Eight.html) curve traced out by $r(\theta) = \cos(2\theta)\sec(\theta)^4$ from $-\pi/4$ to $\pi/4$. Do this numerically.

```
r(theta) = sqrt(cos(2theta) * sec(theta)^4)
val, _ = QuadGK.quadgk(t -> sqrt(D(r)(t)^2 + r(t)^2), -pi/4, pi/4)
numericq(val)
```

